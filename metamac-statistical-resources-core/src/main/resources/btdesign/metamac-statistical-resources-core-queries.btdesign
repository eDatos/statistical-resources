import "classpath:/btdesign/metamac-statistical-resources-core-base.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-datasets.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-queries-enums.btdesign"
import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"

Application QueriesStatisticalResources {
	basePackage=unused

	Module domain_queries {
		basePackage=org.siemac.metamac.statistical.resources.core.query

		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------
		
		Service QueryService {
			> @QueryRepository
			> @QueryVersionRepository
			
			"Retrieve query by urn"
    		@QueryVersion retrieveQueryVersionByUrn(String urn) throws MetamacException;
    		"Retrieve all queries"
    		List<@QueryVersion> retrieveQueryVersions() throws MetamacException;
    		"Find queries by criteria"
    		PagedResult<@QueryVersion> findQueryVersionsByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter)  throws MetamacException;
    		"Create query"
            @QueryVersion createQueryVersion(@QueryVersion queryVersion, @ExternalItem statisticalOperation) throws MetamacException;
            "Update query"
            @QueryVersion updateQueryVersion(@QueryVersion queryVersion) throws MetamacException;
            "Mark query as discontinued because the data is not going to be updated"
            @QueryVersion markQueryVersionAsDiscontinued(@QueryVersion query) throws MetamacException;
            "Delete query"
            deleteQueryVersion(String urn) throws MetamacException;
            "Retrieves last query version by query urn"
			@QueryVersion retrieveLatestQueryVersionByQueryUrn(String queryUrn) throws MetamacException;
			"Retrieve latest published queryVersion providing queryUrn"
    		@QueryVersion retrieveLatestPublishedQueryVersionByQueryUrn(String queryUrn) throws MetamacException;
		}  


		// ----------------------------------------------------------------
		// 							ENTITIES
		// ---------------------------------------------------------------- 

		Entity CodeItem {
			databaseTable="TB_CODE_ITEMS"
			hint="idSequence=CODE_ITEMS"
			!auditable
			gap
		
			"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			String code;
			String title length="4000";
			
			- @QuerySelectionItem querySelectionItem not nullable cascade="none" databaseColumn="QUERY_SELECTION_ITEM_FK" <-> codes;
			
			Repository CodeItemRepository {
				findAll;
			}
		}

		Entity QuerySelectionItem {
			databaseTable="TB_QUERY_SELECTION_ITEMS"
			hint="idSequence=QUERY_SELECTION_ITEMS"
			!auditable
			gap
			
			"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			
			String dimension not nullable;
			- Bag<@CodeItem> codes inverse <-> querySelectionItem not nullable cascade="all-delete-orphan"; 
			- @QueryVersion query not nullable cascade="none" databaseColumn="QUERY_FK" <-> selection;
			
			Repository QuerySelectionItemRepository {
				findAll;
				delete;
			}
		}

		Entity Query {
			databaseTable="TB_QUERIES"
			hint="idSequence=QUERIES"
			!auditable
			
			- @IdentifiableStatisticalResource identifiableStatisticalResource databaseColumn="IDENTIFIABLE_RESOURCE_FK" not nullable cascade="all";
			"All versions"
			- Bag<@QueryVersion> versions cascade="all-delete-orphan" fetch="lazy" inverse <-> query orderby="id asc";
			Integer latestDataNumber nullable;
			
			
			Repository QueryRepository {
				findByCondition;
				save;
				delete;
				@Query retrieveByUrn(String urn) throws MetamacException;
			}
		}
		
		Entity QueryVersion {
			databaseTable="TB_QUERIES_VERSIONS"
			hint="idSequence=QUERIES_VERSIONS"
			!auditable
			gap
			
			- @Query query not nullable cascade="none" databaseColumn="QUERY_FK" <-> versions;
			- @LifeCycleStatisticalResource lifeCycleStatisticalResource databaseColumn="LIFECYCLE_RESOURCE_FK" not nullable cascade="all";
			- @DatasetVersion datasetVersion databaseColumn="DATASET_VERSION_FK" not nullable;
			- @QueryStatusEnum status not nullable;
			- Bag<@QuerySelectionItem> selection inverse <-> query cascade="all-delete-orphan";
			- @QueryTypeEnum type not nullable;
			Integer latestDataNumber nullable;
			
			
			Repository QueryVersionRepository {
				save;
				delete;
				findAll;
				findById;
				findByCondition;
				findByCondition(PagingParameter pagingParameter);
				findByQuery(String query, Map<String, Object> parameters, int maxResult);
				@QueryVersion retrieveByUrn(String urn) throws MetamacException;
				@QueryVersion retrieveLastVersion(String queryUrn) throws MetamacException;
				@QueryVersion retrieveLastPublishedVersion(String queryUrn) throws MetamacException;
			}
		}
	}
}