import "classpath:/btdesign/metamac-statistical-resources-core-base-enums.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-datasets.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-publications.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-queries.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-multidatasets.btdesign"
import "classpath:/btdesign/metamac-core-common-enum.btdesign"

Application CommonStatisticalResources {
	basePackage=unused

	Module core_common {
		basePackage=org.siemac.metamac.statistical.resources.core.common

		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------
		
		Service TranslationService {
			> @TranslationRepository
			
	    	"Translate a SDMX time"
			Map<String,String> retrieveTimeTranslation(String timeSdmx) throws MetamacException;
		}
		
		"Entity for store information about RelatedResources."	
    	Entity RelatedResource {
    		gap
    		
    		databaseTable = "TB_RELATED_RESOURCES"
    		hint="idSequence=RELATED_RESOURCES"
    		not auditable
    		
    		"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			
			- @TypeRelatedResourceEnum type databaseColumn="TYPE" required;
			
			- @DatasetVersion datasetVersion databaseColumn="DATASET_VERSION_FK" cascade="none" nullable fetch="lazy";
			- @Dataset dataset databaseColumn="DATASET_FK" cascade="none" nullable fetch="lazy";
			- @PublicationVersion publicationVersion databaseColumn="PUBLICATION_VERSION_FK" cascade="none" nullable fetch="lazy";
			- @Publication publication databaseColumn="PUBLICATION_FK" cascade="none" nullable fetch="lazy";
			- @QueryVersion queryVersion databaseColumn="QUERY_VERSION_FK" cascade="none" nullable fetch="lazy";
			- @Query query databaseColumn="QUERY_FK" cascade="none" nullable fetch="lazy";
			- @MultidatasetVersion multidatasetVersion databaseColumn="MULTIDATASET_VERSION_FK" cascade="none" nullable fetch="lazy"; 
			- @Multidataset multidataset databaseColumn="MULTIDATASET_FK" cascade="none" nullable fetch="lazy";		
    		
    		Repository RelatedResourceRepository {
				findById;
				delete;
				findAll;
			}
    	}
	
	    "The International String is a collection of Localised Strings and supports the representation of text in multiple locales."
		Entity InternationalString {
			gap
			
			databaseTable = "TB_INTERNATIONAL_STRINGS"
			hint="idSequence=I18NSTRS"
			not auditable
			
			"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			- Set<@LocalisedString> texts cascade="all-delete-orphan" inverse <-> internationalString;
			
			Repository InternationalStringRepository {
				findById;
				delete;
				findAll;
				deleteInternationalStringsEfficiently(Collection<Long> internationalStringToDelete);
			}
		}

		"The Localised String supports the representation of text in one locale (locale is similar to language but includes geographic 
        variations such as Canadian French, US English etc.)."
		Entity LocalisedString {
			gap
			
			databaseTable = "TB_LOCALISED_STRINGS"
			hint="idSequence=L10NSTRS"
			! aggregateRoot
			not auditable

			"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			
			"Label of the string."
			String label length="4000";
			"The geographic locale of the string e.g French, Canadian French."
			String locale;
			"True is LocalisedString cannot be deleted or modified"
			Boolean isUnmodifiable nullable;
			
			"Relation to InternationalString owner"
			- @InternationalString internationalString not nullable cascade="none" databaseColumn="INTERNATIONAL_STRING_FK" <-> texts;
		}
		
		
		"Entity for store information about ExternalItems."	
    	Entity ExternalItem {
    		databaseTable = "TB_EXTERNAL_ITEMS"
    		hint="idSequence=EXTERNAL_ITEMS"
    		not auditable
    		
    		"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			
			String code databaseColumn="CODE" not nullable;
			String codeNested databaseColumn="CODE_NESTED" nullable;
			String uri databaseColumn="URI" not nullable length="4000";
			String urn databaseColumn="URN" nullable length="4000";
			String urnProvider databaseColumn="URN_PROVIDER" nullable length="4000";
			- @TypeExternalArtefactsEnum type databaseColumn="TYPE" not nullable;
			- @InternationalString title cascade="all" databaseColumn="TITLE_FK" nullable;
			String managementAppUrl databaseColumn="MANAGEMENT_APP_URL" nullable length="4000";
    		
    		Repository ExternalItemRepository {
				findById;
				delete;
				findAll;
			}
    	}
    	
        "Translations by code"
		Entity Translation {
			databaseTable="TB_TRANSLATIONS"
			hint="idSequence=TRANSLATIONS"
			not auditable
			not optimisticLocking
			
			"Id. Added explicitly to avoid that Sculptor generate UUID"
			Long id not nullable key;
			"Code"
			String code not nullable;
       		"Title"
       		- @InternationalString title cascade="all" not nullable databaseColumn="TITLE_FK";
			
			Repository TranslationRepository {
            	findByCondition;
            	@Translation findTranslationByCode(String code);
			}
		} 
	}    	
}