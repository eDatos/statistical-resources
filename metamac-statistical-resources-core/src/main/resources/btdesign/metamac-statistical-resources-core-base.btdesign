import "classpath:/btdesign/metamac-statistical-resources-core-base-enums.btdesign"
import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/metamac-core-common-enum.btdesign"

Application BaseStatisticalResources {
	basePackage=unused

	Module core_base {
		basePackage=org.siemac.metamac.statistical.resources.core.base

		"Entity for store information about RelatedResources."	
    	Entity RelatedResource {
    		gap
    		
    		databaseTable = "TB_RELATED_RESOURCES"
    		hint="idSequence=RELATED_RESOURCES"
    		not auditable
    		
    		"Added explicitly to avoid that Sculptor generate UUID"
			Long id key;
			
			String code databaseColumn="CODE" required;
			String uri databaseColumn="URI" required length="4000";
			String urn databaseColumn="URN" required length="4000";
			- @TypeRelatedResourceEnum type databaseColumn="TYPE" required;
			- @InternationalString title cascade="all" databaseColumn="TITLE_FK" nullable;
    		
    		Repository RelatedResourceRepository {
				findById;
				delete;
				findAll;
			}
    	}
    	
    	Entity VersionRationaleType {
    		databaseTable = "TB_VERSION_RATIONALE_TYPE"
    		hint="idSequence=VERSION_RATIONALE_TYPE"
    		not auditable
    		
    		- @StatisticalResourceVersionRationaleTypeEnum value required;
    		
    		Repository VersionRationaleTypeRepository {
				findById;
				delete;
			}
    	}
    	
		"Statistical Resource"
		abstract Entity StatisticalResource {
			databaseTable="TB_STAT_RESOURCES"

			hint="idSequence=STAT_RESOURCES"
			inheritanceType=SINGLE_TABLE

			discriminatorType=STRING

			discriminatorColumn="STAT_RESOURCE_TYPE"
			discriminatorLength="255"
			
			"Last update to optimistic locking"
			DateTimeTZ updateDate nullable;

			Repository StatisticalResourceRepository {
				save;
				delete;
				findAll;
			}

		}

		Entity IdentifiableStatisticalResource extends @StatisticalResource {
			databaseTable="TB_STAT_RESOURCES"
			discriminatorValue="IDENTIFIABLE_RESOURCE"

			String code nullable;
			String uri nullable;
			String urn nullable;

			Repository IdentifiableStatisticalResourceRepository {
				findByCondition;
				@IdentifiableStatisticalResource retrieveByUrn(String urn) throws MetamacException;
				checkDuplicatedUrn(@IdentifiableStatisticalResource identifiableStatisticalResource) throws MetamacException;
			}

		}

		Entity NameableStatisticalResource extends @IdentifiableStatisticalResource {
			databaseTable="TB_STAT_RESOURCES"
			discriminatorValue="NAMEABLE_RESOURCE"

			- @InternationalString title nullable cascade="all" databaseColumn="TITLE_FK";
			- @InternationalString description nullable cascade="all" databaseColumn="DESCRIPTION_FK";
		}

		Entity VersionableStatisticalResource extends @NameableStatisticalResource {
			databaseTable="TB_STAT_RESOURCES"
			discriminatorValue="VERSIONABLE_RESOURCE"

			String versionLogic nullable;
			DateTimeTZ nextVersionDate nullable;
			DateTimeTZ validFrom nullable;
			DateTimeTZ validTo nullable;
			- Bag<@VersionRationaleType> versionRationaleTypes cascade="all-delete-orphan" databaseJoinTable="TB_SR_VRATIONALE_TYPES";
			- @InternationalString versionRationale nullable cascade="all" databaseColumn="VERSION_RATIONALE_FK";
			- @StatisticalResourceNextVersionEnum nextVersion nullable;
			Boolean isLastVersion nullable;
		}

		Entity LifeCycleStatisticalResource extends @VersionableStatisticalResource {
			databaseTable="TB_STAT_RESOURCES"
			discriminatorValue="LIFE_CYCLE_RESOURCE"

			- @StatisticalResourceProcStatusEnum procStatus nullable;
			
			DateTimeTZ creationDate nullable;
			String creationUser nullable;
			DateTimeTZ productionValidationDate nullable;
			String productionValidationUser nullable;
			DateTimeTZ diffusionValidationDate nullable;
			String diffusionValidationUser nullable;
			DateTimeTZ rejectValidationDate nullable;
			String rejectValidationUser nullable;
			DateTimeTZ internalPublicationDate nullable;
			String internalPublicationUser nullable;
			DateTimeTZ externalPublicationDate nullable;
			String externalPublicationUser nullable;
			
			Boolean externalPublicationFailed nullable;
			DateTimeTZ externalPublicationFailedDate nullable;
			
				
			- @RelatedResource replacesVersion cascade="all" databaseColumn="REPLACES_VERSION_FK" nullable;
			- @RelatedResource isReplacedByVersion cascade="all" databaseColumn="IS_REPLACED_BY_VERSION_FK" nullable;
		}

		Entity SiemacMetadataStatisticalResource extends @LifeCycleStatisticalResource {
			databaseTable="TB_STAT_RESOURCES"
			discriminatorValue="SIEMAC_RESOURCE"
			
			//LANGUAGES
			- @ExternalItem language nullable cascade="all" databaseColumn="LANGUAGE_FK";
			- Bag<@ExternalItem> languages cascade="all-delete-orphan" fetch="lazy" databaseJoinTable="TB_SR_LANGUAGES";
			
			//THEMATIC CONTENT CLASSIFIER
			- @ExternalItem statisticalOperation nullable cascade="all" databaseColumn="STAT_OPERATION_FK";
			- @ExternalItem statisticalOperationInstance nullable cascade="all-delete-orphan" databaseColumn="STAT_OPERATION_INST_FK";
			
			//CONTENT DESCRIPTORS
			- @InternationalString subtitle cascade="all" databaseColumn="SUBTITLE_FK" nullable;
			- @InternationalString titleAlternative cascade="all" databaseColumn="TITLE_ALTERNATIVE_FK" nullable;
			- @InternationalString abstractLogic cascade="all" databaseColumn="ABSTRACT_FK" nullable;
			//keywords type??
			
			//CLASS DESCRIPTORS
			- @StatisticalResourceTypeEnum type nullable;
			
			//PRODUCTION DESCRIPTORS
			- @ExternalItem maintainer cascade="all" databaseColumn="MAINTAINER_FK" nullable;
			- @ExternalItem creator cascade="all" databaseColumn="CREATOR_FK" nullable;
			- Bag<@ExternalItem> contributor cascade="all-delete-orphan" databaseColumn="CONTRIBUTOR_FK" databaseJoinTable="TB_SR_CONTRIBUTORS";
			DateTimeTZ resourceCreatedDate nullable;
			DateTimeTZ lastUpdate nullable;
			- @InternationalString conformsTo cascade="all" databaseColumn="CONFORMS_TO_FK" nullable;
			- @InternationalString conformsToInternal cascade="all" databaseColumn="CONFORMS_TO_INTERNAL_FK" nullable;
			
			//PUBLISHING DESCRIPTORS
			- Bag<@ExternalItem> publisher cascade="all-delete-orphan" databaseColumn="PUBLISHER_FK" databaseJoinTable="TB_SR_PUBLISHERS";
			- Bag<@ExternalItem> publisherContributor cascade="all-delete-orphan" databaseColumn="PUBLISHER_FK" databaseJoinTable="TB_SR_PUB_CONTRIBUTORS";
			- Bag<@ExternalItem> mediator cascade="all-delete-orphan" databaseColumn="MEDIATOR_FK" databaseJoinTable="TB_SR_MEDIATORS";
			DateTimeTZ newnessUntilDate nullable;
			
			//RESOURCE RELATIONS DESCRIPTORS
			- @RelatedResource replaces cascade="all" databaseColumn="REPLACES_FK" nullable;
			- @RelatedResource isReplacedBy cascade="all" databaseColumn="IS_REPLACED_BY_FK" nullable;
			- Bag<@RelatedResource> requires cascade="all-delete-orphan" databaseColumn="REQUIRES_FK" databaseJoinTable="TB_SR_REQUIRES";
			- Bag<@RelatedResource> isRequiredBy cascade="all-delete-orphan" databaseColumn="IS_REQUIRED_BY_FK" databaseJoinTable="TB_SR_IS_REQUIRED_BY";
			- Bag<@RelatedResource> hasPart cascade="all-delete-orphan" databaseColumn="HAS_PART_FK" databaseJoinTable="TB_SR_HAS_PART";
			- Bag<@RelatedResource> isPartOf cascade="all-delete-orphan" databaseColumn="IS_PART_OF_FK" databaseJoinTable="TB_SR_IS_PART_OF";
			
			//INTELLECTUAL PROPERTY DESCRIPTORS
			- @ExternalItem rightsHolder cascade="all" databaseColumn="RIGHTS_HOLDER_FK" nullable;
			DateTimeTZ copyrightedDate	nullable;
			- @InternationalString license cascade="all" databaseColumn="LICENSE_FK" nullable;
			- @InternationalString accessRights cascade="all" databaseColumn="ACCESS_RIGHTS_FK" nullable;
			
			Repository SiemacMetadataStatisticalResourceRepository {
				String findLastUsedCodeForResourceType(String operationUrn, @StatisticalResourceTypeEnum resourceType);
			}
			
		}

	}

}