import "classpath:/btdesign/metamac-core-common-dtos.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-common-dtos.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-base-dtos.btdesign"
import "classpath:/btdesign/metamac-statistical-resources-core-datasets-enums.btdesign"

Application DatasetsStatisticalResourcesDto {
	basePackage=unused

	Module dtos_datasets {
		basePackage=org.siemac.metamac.statistical.resources.core.dto.datasets 
		
		DataTransferObject DatasetVersionBaseDto extends @SiemacMetadataStatisticalResourceBaseDto {
			- @ExternalItemDto relatedDsd;
			- @StatisticOfficialityDto statisticOfficiality;
			
			// If a task related with the dataset is being executed in background
			Boolean isTaskInBackground;
		}
		
		DataTransferObject DatasetVersionDto extends @SiemacMetadataStatisticalResourceDto {
			// geographic granularities
			- Bag<@ExternalItemDto> geographicGranularities;
			// temporal granularities
			- Bag<@ExternalItemDto> temporalGranularities;
			// First date which data refers to
			JAVADATE dateStart;
			// Last date which data refers to
			JAVADATE dateEnd;
			- Bag<@ExternalItemDto> statisticalUnit;

			//Identifiers
			String datasetRepositoryId;

			//Production descriptor

			- @ExternalItemDto relatedDsd;

			//Class descriptors

			//Number of observations contained in the dataset
			Long formatExtentObservations;
			//Number of diemnsions defined in the DSD
			Integer formatExtentDimensions;

			//Publishing descriptors
			
			//relation descriptors
			- Bag<@RelatedResourceDto> isRequiredBy;
			- Bag<@RelatedResourceDto> isPartOf;

			//public date of next update
			JAVADATE dateNextUpdate;
			//Frecuency which the data is updated
			- @ExternalItemDto updateFrequency;
			//Official/no official statistic
			- @StatisticOfficialityDto statisticOfficiality;
			- @InternationalStringDto bibliographicCitation;
			
			// If a task related with the dataset is being executed in background
			Boolean isTaskInBackground;
		}

		DataTransferObject DatasourceDto extends @IdentifiableStatisticalResourceDto {
			"Parent"
			String datasetVersionUrn;
		}	

		"Dto for Statistic officiality"
        DataTransferObject StatisticOfficialityDto extends @IdentityDto {
            String identifier;
            - @InternationalStringDto description nullable;
        }

		"Dto for temporal code"
        DataTransferObject TemporalCodeDto {
            String identifier;
            String title;
        }

        DataTransferObject DatasetVersionMainCoveragesDto {
            - Bag<@ExternalItemDto> geographicCoverage;
            - Bag<@TemporalCodeDto> temporalCoverage;
            - Bag<@ExternalItemDto> measureCoverage;
        }

         DataTransferObject DsdDimensionDto extends @IdentityDto {
        	String dimensionId;
        	- @DimensionTypeEnum type;
        	String codelistRepresentationUrn;
        	String conceptSchemeRepresentationUrn;
        }

         DataTransferObject ItemDto extends @ExternalItemDto {
        	String itemParentUrn;
        }

        DataTransferObject DsdAttributeDto extends @IdentityDto {
        	String identifier;
        	- @RelationshipDto attributeRelationship;
        	- @RepresentationDto attributeRepresentation;
        }

        DataTransferObject RelationshipDto extends @IdentityDto {
        	- @AttributeRelationshipTypeEnum relationshipType;
			List<String> dimensions;
			String group;
			List<String> groupDimensions;
        }
        
        DataTransferObject RepresentationDto extends @IdentityDto {
        	- @AttributeRepresentationTypeEnum representationType;
        	String codelistRepresentationUrn;
        	String conceptSchemeRepresentationUrn;
        }

        DataTransferObject DsdAttributeInstanceDto extends @IdentityDto {
        	String attributeId;
			CodeDimensionMap codeDimensions;
			-@AttributeValueDto value;
        }
        
        DataTransferObject AttributeValueDto extends @IdentityDto {
        	- @ExternalItemDto externalItemValue;
        	String stringValue;
        }
        
        DataTransferObject CategorisationDto extends @VersionableStatisticalResourceDto  {
        	- @ExternalItemDto category not nullable;
        	- @ExternalItemDto maintainer not nullable;
        	- @RelatedResourceDto datasetVersion not nullable;
        }
	}
}
